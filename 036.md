Custom Gunmetal Woodpecker

Medium

# Sudden Fee Increase Will Cause PIN Loss for Sellers

### Summary

The lack of a delay mechanism in `PinlinkShop.setFee()` will cause a financial loss in PIN tokens for sellers as any sudden fee can potentially increase the purchase fee percentage just before a buyer executes a purchase, reducing the sellerâ€™s expected payment without warning.

### Root Cause

In [pinlinkShop.sol:174](https://github.com/sherlock-audit/2025-03-pinlink-rwa-tokenized-depin-marketplace/blob/main/marketplace-contracts/src/marketplaces/pinlinkShop.sol#L174C7-L174C11), the setFee function allows the admin to immediately update purchaseFeePerc without a delay or notification period, enabling sudden fee increases that impact ongoing listings.

### Internal Pre-conditions

1. Admin needs to call `setFee()` to set `purchaseFeePerc` to go from 500 (5%) to 1000 (10%).
2. Seller needs to have an active listing in PinlinkShop with a set `usdPricePerFraction` expecting the 5% fee rate.

### External Pre-conditions

1. Buyer needs to execute a purchase transaction within the same block as the fee increase or a short time after.

### Attack Path

1. Seller (e.g., Bob) lists 20 fractional assets for 1100e18 USD each, expecting a 5% fee and ~22,000e18 PIN payment.
2. Admin calls `setFee(1000)` to increase the fee to 10%, effective immediately.
3. Buyer (e.g., Alice) calls `purchase(bobListingId, 20, 23,157.8947e18)` to buy the assets, triggering the new 10% fee.
4. Seller receives ~20,842e18 PIN instead of 22,000e18 due to the increased fee and lack of a notification period, losing ~1,157e18 PIN.

### Impact

Sellers who cannot adjust their listing prices in response to the fee increase will incur a substantial loss.

### PoC

```solidity

// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.27;

import "forge-std/console.sol";
import {FractionalAssets} from "src/fractional/FractionalAssets.sol";
import {PinlinkShop, Listing} from "src/marketplaces/pinlinkShop.sol";
import {Test} from "forge-std/Test.sol";
import {IERC20} from "forge-std/interfaces/IERC20.sol";
import {CentralizedOracle} from "src/oracles/CentralizedOracle.sol";

contract SuddenFeeIncreaseSellerLoss is Test {
    FractionalAssets fractions;
    PinlinkShop pshop;
    CentralizedOracle oracle;

    address admin;
    address alice;
    address bob;
    uint256 alicePrivateKey;
    uint256 bobPrivateKey;

    uint256 asset1 = 111;

    IERC20 PIN = IERC20(0x2e44f3f609ff5aA4819B323FD74690f07C3607c4); // Mainnet PIN
    IERC20 USDC = IERC20(0xA0b86991c6218b36c1d19D4a2e9Eb0cE3606eB48); // Mainnet USDC

    uint256 aliceStartingBalance = 100_000e18;
    bytes32 initialListingId;
    bytes32 bobListingId;

    function setUp() public {
        string memory MAINNET_RPC_URL = vm.envString("MAINNET_RPC_URL");
        vm.createSelectFork(MAINNET_RPC_URL, 21947000);

        admin = makeAddr("admin");
        (alice, alicePrivateKey) = makeAddrAndKey("alice");
        (bob, bobPrivateKey) = makeAddrAndKey("bob");

        vm.startPrank(admin);
        oracle = new CentralizedOracle(address(PIN), 0.95 ether); // 1 PIN = 0.95 USD
        fractions = new FractionalAssets(
            "https://metadata.pinlink.dev/metadata/0xaaaa/"
        );
        fractions.mint(asset1, admin, 100);
        pshop = new PinlinkShop(address(PIN), address(oracle), address(USDC));
        fractions.setApprovalForAll(address(pshop), true);
        vm.stopPrank();

        deal(address(PIN), alice, aliceStartingBalance);
        vm.prank(alice);
        PIN.approve(address(pshop), type(uint256).max);

        deal(address(PIN), bob, 100_000e18);
        vm.prank(bob);
        PIN.approve(address(pshop), type(uint256).max);

        uint supply = 20;
        vm.startPrank(admin);
        pshop.enableAsset(address(fractions), asset1, admin);
        initialListingId = pshop.list(
            address(fractions),
            asset1,
            supply,
            1000e18,
            block.timestamp + 7 days
        );
        vm.stopPrank();

        uint256 pinAmount = pshop.getQuoteInTokens(initialListingId, supply);
        vm.startPrank(bob);
        pshop.purchase(initialListingId, supply, pinAmount);
        bobListingId = pshop.list(
            address(fractions),
            asset1,
            supply,
            1100e18,
            block.timestamp + 3 days
        );
        vm.stopPrank();
    }

    function test_sellerLosesPinDueToFeeIncrease() public {
        
        // Bob's balance after buying from admin
        uint256 bobPinBalanceBefore = PIN.balanceOf(bob); // ~78,947.3684e18 after initial purchase

        // Bob's listing: 20 tokens at 1100e18 USD each
        uint256 expectedPinPayment = pshop.getQuoteInTokens(bobListingId, 20);
        assertApproxEqAbs(
            expectedPinPayment,
            23_157.8947e18,
            1e18,
            "PIN quote mismatch"
        );

        // At 5% fee (default), Bob expects:
        uint256 expectedPaymentAt5Percent = (expectedPinPayment * 9500) /
            10_000; // 22,000e18 PIN
        assertApproxEqAbs(
            expectedPaymentAt5Percent,
            22_000e18,
            1e18,
            "5% fee calc mismatch"
        );

        // Admin increases fee to 10%
        vm.prank(admin);
        pshop.setFee(1000); // 10%

        // Alice purchases Bob's listing
        vm.startPrank(alice);
        uint256 alicePinBalanceBefore = PIN.balanceOf(alice);
        (uint256 aliceFractionBalanceBefore, , ) = pshop.getBalances(
            address(fractions),
            asset1,
            alice
        );
        pshop.purchase(bobListingId, 20, expectedPinPayment);
        uint256 alicePinBalanceAfter = PIN.balanceOf(alice);
        (uint256 aliceFractionBalanceAfter, , ) = pshop.getBalances(
            address(fractions),
            asset1,
            alice
        );
        vm.stopPrank();

        // Bob's actual payment at 10% fee
        uint256 actualPaymentAt10Percent = (expectedPinPayment * 9000) / 10_000; // 20,842.1053e18 PIN
        uint256 bobPinBalanceAfter = PIN.balanceOf(bob);
        assertApproxEqAbs(
            bobPinBalanceAfter - bobPinBalanceBefore,
            actualPaymentAt10Percent,
            1e18,
            "Bob payment mismatch"
        );

        // Loss in PIN
        uint256 pinLoss = expectedPaymentAt5Percent - actualPaymentAt10Percent; // ~1,157.8947e18 PIN
        assertApproxEqAbs(
            pinLoss,
            1157.8947e18,
            1e18,
            "PIN loss calc mismatch"
        );

        // Loss in USD: pinLoss * 0.95 USD/PIN
        uint256 usdLoss = (pinLoss * 95) / 100; // ~1,100e18 USD
        assertApproxEqAbs(usdLoss, 1100e18, 1e18, "USD loss mismatch");

        // Verify Alice paid full amount
        uint256 alicePinSpent = alicePinBalanceBefore - alicePinBalanceAfter;
        assertApproxEqAbs(
            alicePinSpent,
            expectedPinPayment,
            1e18,
            "Alice payment mismatch"
        );

        // Verify Alice received 20 fractional tokens
        uint256 aliceFractionsReceived = aliceFractionBalanceAfter -
            aliceFractionBalanceBefore;
        assertEq(
            aliceFractionsReceived,
            20,
            "Alice did not receive expected fractions"
        );

        console.log(
            "Bob expected PIN (5% fee):",
            expectedPaymentAt5Percent / 1e18,
            "PIN"
        );
        console.log(
            "Bob received PIN (10% fee):",
            actualPaymentAt10Percent / 1e18,
            "PIN"
        );
        console.log("Bob lost PIN:", pinLoss / 1e18, "PIN");
        console.log("Bob lost USD:", usdLoss / 1e18, "USD");
    }
}
``` 

### Mitigation

Implement a delay mechanism for fee changes in PinlinkShop to give sellers time to adjust listings:

```solidity
uint256 public feeUpdateTimestamp;
uint256 public pendingFeePerc;

function setFee(uint256 newFee) external onlyRole(DEFAULT_ADMIN_ROLE) {
    require(newFee <= MAX_FEE_PERC, "InvalidParameter");
    pendingFeePerc = newFee;
    feeUpdateTimestamp = block.timestamp + 1 days; // 24-hour delay
    emit FeePercentageSet(newFee); // Emit early for transparency
}

function _handlePayments(address buyer, address seller, uint256 totalPinPayment) internal returns (uint256, uint256) {
    uint256 currentFee = (block.timestamp >= feeUpdateTimestamp) ? pendingFeePerc : purchaseFeePerc;
    uint256 paymentForSeller = totalPinPayment * (FEE_DENOMINATOR - currentFee) / FEE_DENOMINATOR;
    uint256 fees = totalPinPayment - paymentForSeller;
    IERC20(PIN).safeTransferFrom(buyer, seller, paymentForSeller);
    IERC20(PIN).safeTransferFrom(buyer, feeReceiver, fees);
    return (paymentForSeller, fees);
}
```

Another mitigation would be adding the fee directly to the listing struct and not mutating it after initialization. 