Mysterious Pistachio Platypus

Medium

# The reward distribution can be DOS'ed by updating the reward of the REWARD_PROXY_ACCOUNT frequently.

### Summary

When rewards are distributed, they are distributed equally per asset and per block over a period via depositRewards(). If the reward amount is low enough and the period long enough, an attacker could get the reward tokens stuck in the contract by updating the reward of the REWARD_PROXY_ACCOUNT frequently. 

### Root Cause

In [depositRewards()](https://github.com/sherlock-audit/2025-03-pinlink-rwa-tokenized-depin-marketplace/blob/main/marketplace-contracts%2Fsrc%2Fmarketplaces%2Fstreams.sol#L87).
```solidity
    function depositRewards(RewardsStream storage self, uint256 amount, uint256 drippingPeriod) internal {
        if (drippingPeriod > MAX_DRIPPING_PERIOD) revert PinlinkRewards_DrippingPeriodTooLong();
        if (drippingPeriod < MIN_DRIPPING_PERIOD) revert PinlinkRewards_DrippingPeriodTooShort();

        if (!self.isDrippingPeriodFinished()) revert PinlinkRewards_DepositRewardsTooEarly();
        if (!self.isEnabled()) revert PinlinkRewards_AssetNotEnabled();

        // This ensures rounding errors are negligible (less than 0.01$ per deposit)
        if (amount < MIN_REWARDS_DEPOSIT_AMOUNT) revert PinlinkRewards_AmountTooLow();

        // The number of fractions per asset is expected to be on the order of 100.
        // Thus, precision loss will usually be negligible (on the order of less than 100 wei)
        // Therefore, precision loss is deliberately ignored here to save gas
        uint256 delta = (amount * PRECISION) / self.assetSupply;
        /// The dripping mechanism is to avoid step jumps in rewards
        self.globalRewardsPerStakedTarget += delta;
        self.deltaGlobalRewardsPerStaked = delta;
        self.lastDepositTimestamp = block.timestamp;
        self.drippingPeriod = drippingPeriod;
    }
```
When rewards are distributed, they are distributed per block using [globalRewardPerStaked()](https://github.com/sherlock-audit/2025-03-pinlink-rwa-tokenized-depin-marketplace/blob/main/marketplace-contracts%2Fsrc%2Fmarketplaces%2Fstreams.sol#L112), to avoids step-wise jumps in distribution. The amount is scaled up by 1e18 and divided by the `assetSupply` to get the reward token amount per asset.
```solidity
    function globalRewardsPerStaked(RewardsStream storage self) internal view returns (uint256) {
        if (self.lastDepositTimestamp == 0) return 0;

        // safe subtraction as is always less or equal to block.timestamp
        uint256 timeSinceDeposit = block.timestamp - self.lastDepositTimestamp;

        uint256 _drippingDuration = self.drippingPeriod;
        // if the _drippingDuration has passed, then locked is 0
        // at deposit, locked has to be deltaGlobalRewardsPerStaked
        // during the _drippingDuration, locked is an interpolation between deltaGlobalRewardsPerStaked and 0
        uint256 locked = (timeSinceDeposit > _drippingDuration)
            ? 0
            : self.deltaGlobalRewardsPerStaked * (_drippingDuration - (timeSinceDeposit)) / _drippingDuration;

        /// return the target after _drippingDuration, and before that, an interpolation between last and new target
        return self.globalRewardsPerStakedTarget - locked;
    }
```
When the rewards are updated via [updateRewards()](https://github.com/sherlock-audit/2025-03-pinlink-rwa-tokenized-depin-marketplace/blob/main/marketplace-contracts%2Fsrc%2Fmarketplaces%2Fstreams.sol#L130) for users or REWARD_PROXY_ADDRESS, they get the difference of the globalRewardsPerStakedTarget and updatedRewardPerStake. If the difference low such that assetSupply * difference < 1e18, it would add 0 to the pendingReward mapping but update the updatedRewardPerStake, losing the rewards.
```solidity
    function updateRewards(RewardsStream storage self, address account) internal {
        uint256 globalPerStaked = self.globalRewardsPerStaked();
        self.pendingRewards[account] += self._pendingRewardsSinceLastUpdate(globalPerStaked, account);
        self.updatedRewardsPerStaked[account] = globalPerStaked;
    }

    function _pendingRewardsSinceLastUpdate(RewardsStream storage self, uint256 globalPerStaked, address account)
        internal
        view
        returns (uint256)
    {
        // this can't underflow, because this always holds: `globalRewardsPerStaked() >= updatedRewardsPerStaked[account]`
        return (self.stakedBalances[account] * (globalPerStaked - self.updatedRewardsPerStaked[account])) / PRECISION;
    }
}
```
Lets say the REWARD_PROXY_ACCOUNT has all the assets supply and gets all the rewards, if the rewards are low and dripping period is high, such that every block, the drip amount is less than or ~1e16, updating rewards every block while the calculation (     (self.stakedBalances[account] * (globalPerStaked - self.updatedRewardsPerStaked[account])) / PRECISION;) would be 0 makes the account gets 0 rewards but its updatedRewardsPerStaked up-to-date. This means the rewards would be stuck on the contract. An attacker could call the [withdrawAsset()](https://github.com/sherlock-audit/2025-03-pinlink-rwa-tokenized-depin-marketplace/blob/main/marketplace-contracts%2Fsrc%2Fmarketplaces%2FpinlinkShop.sol#L352)would amount as 0 to bypass the check and execute transferBalance() on the REWARD_PROXY_ACCOUNT. 

```solidity
    function withdrawAsset(address fractionalAssets, uint256 tokenId, uint256 amount, address receiver) external {
        if (_nonListedBalance(fractionalAssets, tokenId, msg.sender) < amount) revert NotEnoughUnlistedTokens();

        // this does't transfer the assets, but only the internal accounting of staking balances
        streams[fractionalAssets][tokenId].transferBalances(msg.sender, REWARDS_PROXY_ACCOUNT, amount);

        emit FractionsWithdrawn(fractionalAssets, tokenId, amount, receiver);
        IFractionalAssets(fractionalAssets).safeTransferFrom(address(this), receiver, tokenId, amount, "");
    }
```
An attacker could execute this frequently to make the updatedRewardsPerStaked of REWARD_PROXY_ACCOUNT up-to-date but not the reward amount. 


### Internal Pre-conditions

1. Admin needs to deposit a low amount of rewards over a long duration, such that (amount dripped per block * assetSupply of REWARDS_PROXY_ACCOUNT < 1e18).

### External Pre-conditions

none.

### Attack Path

Admin deposits a low amount of rewards over a long duration, such that (amount dripped per block * assetSupply of REWARDS_PROXY_ACCOUNT < 1e18).

The attacker calls withdrawAsset() with 0 as amount every block to bypass the check and trigger updateRewards() on the REWARDS_PROXY_ACCOUNT via transferBalances().

A 0 amount is added to 'pendingRewards' of RPA but the updatedRewardPerStaked of RPA is updated. 

This means the RPA cant claim the rewards and reward tokens are locked in the contract.

### Impact

The RPA cant claim the rewards and reward tokens will be locked in the contract.

### PoC

_No response_

### Mitigation

Dont let 0 amount of withdrawals.