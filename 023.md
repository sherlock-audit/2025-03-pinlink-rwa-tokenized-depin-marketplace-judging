Soaring Fuzzy Falcon

Medium

# Buyers will experience purchase failures due to PIN token transfer restrictions

### Summary

The PIN token's transfer restrictions will cause purchase failures for buyers as the system will revert when transferring tokens beyond allowed limits.

### Root Cause

In the PinlinkShop contract, the purchase function triggers [`_handlePayments`](https://github.com/sherlock-audit/2025-03-pinlink-rwa-tokenized-depin-marketplace/blob/34cd70b66f1dbe191bd6f946e42f58eef4a00166/marketplace-contracts/src/marketplaces/pinlinkShop.sol#L534) which immediately transfers PIN tokens from the buyer to the seller and feeReceiver:

```solidity
function _handlePayments(
    address buyer,
    address seller,
    uint256 totalPinPayment
) internal returns (uint256 paymentForSeller, uint256 fees) {
    // fees are rounded in favor of the protocol
    paymentForSeller =
        (totalPinPayment * (FEE_DENOMINATOR - purchaseFeePerc)) /
        FEE_DENOMINATOR;
    fees = totalPinPayment - paymentForSeller;

    // no need to verfy that msg.value==0, because purchases with tokens are done with purchaseWithToken() which is non-payable
    IERC20(PIN).safeTransferFrom(buyer, seller, paymentForSeller);
    IERC20(PIN).safeTransferFrom(buyer, feeReceiver, fees);
}
```

However, the [PIN Token](https://vscode.blockscan.com/ethereum/0x2e44f3f609ff5aA4819B323FD74690f07C3607c4) contract imposes restrictions that can cause these transfers to fail. The `_transfer` function includes:

- A `maxWalletRatio` check: `balanceOf(to) + amount <= totalTokens / maxWalletRatio`, preventing recipients from holding more than a certain percentage of the total token supply.
- A `maxTxRatio` check: `amount <= totalTokens / maxTxRatio`, limiting the amount transferable in a single transaction.

These restrictions can lead to reverts when purchasing large amounts of `fractionsAmount` or fractions with a high `usdPricePerFraction`.

```solidity
function _transfer(address from, address to, uint256 amount) private {

@>    // Max tx check
@>    require(amount <= totalTokens / maxTxRatio, "Max tx exceeded.");

      ...

      // Still check max wallet
@>    require(
@>        balanceOf(to) + amount <= totalTokens / maxWalletRatio,
@>        "Max wallet will be exceeded."
      );

      ...

    _tokenTransfer(from, to, amount, _taxAmt, isSell);
}
```

### Internal Pre-conditions

1. The buyer attempts to purchase fractions where the required PIN token transfer exceeds the maxTxRatio limit.

2. The seller or feeReceiver has an existing balance that, when added to the payment, exceeds the maxWalletRatio limit.

### External Pre-conditions

The PIN token contract’s maxTxRatio and maxWalletRatio are set to values that can be exceeded by legitimate purchase transactions.

### Attack Path

1. A seller lists fractions with a high `usdPricePerFraction` or a buyer attempts to buy a large amount of fractions.

2. The buyer calls the `purchase` function, triggering a large PIN token transfer.

3. The contract attempts to transfer PIN tokens from the buyer to the seller and feeReceiver.

4. The PIN token contract reverts due to exceeding `maxTxRatio` or `maxWalletRatio` limits.

5. The purchase fails, preventing the buyer from completing the transaction.

### Impact

Buyers cannot complete their purchases for certain valid transactions due to PIN token transfer restrictions, affecting the core functionality of the PinlinkShop contract.

### PoC

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.27;

import {BaseStakingShopTests} from "test/pinlinkShop/base.t.sol";
import {PinlinkShop, Listing} from "src/marketplaces/pinlinkShop.sol";
import {IPinToken} from "src/interfaces/IPinToken.sol";

contract myPoC is BaseStakingShopTests {
    bytes32 listingId0;
    bytes32 listingId1;
    bytes32 listingId2;
    bytes32 listingId3;

    modifier depositRewards(uint256 amount) {
        vm.prank(operator);
        pshop.depositRewards(address(fractions), asset1, amount, 6 hours);
        // let some time pass so that some rewards are dripped
        vm.warp(block.timestamp + 4 hours);
        _;
    }

    function setUp() public override {
        super.setUp();
        vm.startPrank(admin);
        pshop.enableAsset(address(fractions), asset1, admin);
        pshop.enableAsset(address(fractions), asset2, admin);
        listingId0 = pshop.list(address(fractions), asset1, 70, 8000e18, block.timestamp + 12 days);
        listingId1 = pshop.list(address(fractions), asset1, 30, 10000e18, block.timestamp + 12 days);
        listingId2 = pshop.list(address(fractions), asset2, 100, 2000e18, block.timestamp + 5 days);
        vm.stopPrank();

        // she needs a lot of funds to purchase 30 fractions
        deal(address(PIN), alice, 10_000_000e18);
        _doPurchase(listingId0, 30, alice);
        vm.prank(alice);
        listingId3 = pshop.list(address(fractions), asset1, 25, 900e18, block.timestamp + 3 days);
    }

    // The purchase may fail based on MaxWalletRatio settings
    function testMaxWalletRatioPoC() public {
        // 1. Set the MaxWalletRatio of the PIN token to a high value.
        vm.prank(pinOwner);
        PINInterface(address(PIN)).setMaxWalletRatio(uint32(999));

        // 2. Bob attempts to purchase 25 fractions listed by Alice but fails.
        vm.prank(bob);
        vm.expectRevert("Max wallet will be exceeded.");
        pshop.purchase(listingId3, 25, type(uint256).max);

        // 3. Set the MaxWalletRatio of the PIN token to a low value.
        vm.prank(pinOwner);
        PINInterface(address(PIN)).setMaxWalletRatio(uint32(1));

        // 4. Bob successfully purchases 25 fractions listed by Alice.
        vm.prank(bob);
        pshop.purchase(listingId3, 25, type(uint256).max);
    }

    // The purchase may fail based on MaxTxRatio settings
    function testMaxTxRatioPoC() public {
        // 1. Set the MaxTxRatio of the PIN token to a high value.
        vm.prank(pinOwner);
        PINInterface(address(PIN)).setMaxTxRatio(uint32(999));

        // 2. Bob attempts to purchase 60 fractions listed by admin but fails.
        vm.prank(bob);
        vm.expectRevert("Max tx exceeded.");
        pshop.purchase(listingId2, 60, type(uint256).max);

        // 3. Set the MaxWalletRatio of the PIN token to a low value.
        vm.prank(pinOwner);
        PINInterface(address(PIN)).setMaxTxRatio(uint32(1));

        // 4. Bob successfully purchases 60 fractions listed by admin.
        vm.prank(bob);
        pshop.purchase(listingId3, 25, type(uint256).max);
    }

}

interface PINInterface {
    function setMaxWalletRatio(uint32 ratio) external;
    function setMaxTxRatio(uint32 ratio) external;
}
```

```bash
Ran 2 tests for test/pinlinkShop/myPoC.t.sol:myPoC
[PASS] testMaxTxRatioPoC() (gas: 241689)
[PASS] testMaxWalletRatioPoC() (gas: 232966)
Suite result: ok. 2 passed; 0 failed; 0 skipped; finished in 628.77ms (1.30ms CPU time)

Ran 1 test suite in 633.64ms (628.77ms CPU time): 2 tests passed, 0 failed, 0 skipped (2 total tests)
```

### Mitigation

Instead of transferring PIN tokens directly to the seller and feeReceiver during purchase, store them in the contract or a whitelist escrow without restrictions, allowing the seller and feeReceiver to claim them later in smaller amounts that comply with the PIN token’s limits.