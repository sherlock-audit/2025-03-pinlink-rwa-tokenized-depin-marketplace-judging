Scrawny Blonde Seahorse

Medium

# Leftover rewards can break the 0.01% invariant in suboptimal conditions

### Summary

According to the README, there is an invariant regarding rewards solvency:

"
    the addition of all getPendingRewards() from all accounts and assets should be less or equal to the contract balance of USDC.
    When the current drippingPeriod is finished, it should be equal. A small rounding error is accepted (less than 0.01 % for instance).
"

The invariant specifies that 0.01% is an acceptable loss of the provided rewards over the course of a single drippingPeriod. This invariant is in place to help prevent funds from becoming stuck in the contract. However, with suboptimal values of the totalSupply of a fractional asset and the amount of rewards deposited, the lost amount of funds can be up to 0.99% of the total rewards, well in excess of the 0.01% specified in the README.

### Root Cause

In [streams.sol](https://github.com/sherlock-audit/2025-03-pinlink-rwa-tokenized-depin-marketplace/blob/main/marketplace-contracts/src/marketplaces/streams.sol#L47-L50), there are limits on the size of an asset's supply and the size of a reward deposit.
```solidity
    uint256 constant MAX_ASSET_SUPPLY = 1e4;

    // a minimum of 1 USDC deposited as rewards every time.
    uint256 constant MIN_REWARDS_DEPOSIT_AMOUNT = 1e6;
```

This is by design to make sure that too much supply doesn't divide too little assets, leaving significant amounts behind. However, despite these limits and the clever upscaling math used when calculating rewards, it is possible for up to 0.99% of the total reward to be stuck in the contract. In a perfectly suboptimal situation, an asset with 9999 shares is distributed among 9999 users and has a reward worth 1.009898 USDC added to the contract. In this case, each user would be able to claim 100 wei of USDC at the end, leaving 9998 wei of USDC in the contract. This is 0.99% of the initial deposit.

### Internal Pre-conditions

1. There must be a fractional asset with a high supply
2. There must be a distribution with a low value
3. The supply of the fractional asset must be distributed among many users

### External Pre-conditions

N/A

### Attack Path

N/A

### Impact

The protocol will accumulate small amounts of USDC that are stuck forever in the contract over time. In most cases this likely isn't a huge deal, but it breaks the invariant set in the README, and over enough time a substantial amount of inaccessible USDC will pool in the contract even if far more ideal conditions are the norm.

### PoC

Please copy and paste this into claim.t.sol. The POC is unfortunately slightly limited because doing a for loop for every single user cost too much memory and wouldn't run. However, extrapolating the values from the first few users should be enough to prove that the invariant is broken.

The POC uses claimRewards() instead of getPendingRewards() but the math is the same.

```solidity
    function test_invariant_break() public {
        // create new fractional token with maximum amount of allowed fractions
        vm.startPrank(admin);
        fractions.mint(77777, admin, 9999);
        pshop.enableAsset(address(fractions), 77777, admin);

        // rewards balance before
        uint256 shopBalanceBefore = USDC.balanceOf(address(pshop));

        // create distribution with low and suboptimal amount of coins
        vm.startPrank(operator);
        pshop.depositRewards(address(fractions), 77777, 1_009898, 6 hours);

        // list all shares
        vm.startPrank(admin);
        bytes32 listing = pshop.list(address(fractions), 77777, 9999, 10000, block.timestamp + 7 days);

        // have 10 users buy 1 share each
        for (uint i = 0; i < 10; ++i) {               // magic value to avoid collisions
            address newUser = makeAddr(vm.toString(i + 34534534534545));
            assertEq(USDC.balanceOf(newUser), 0); // make sure user starts with no USDC
            deal(address(PIN), newUser, 1000000);
            vm.startPrank(newUser);
            PIN.approve(address(pshop), 1000000);
            pshop.purchase(listing, 1, 1000000);
        }

        // wait until the end of the distribution, then have all those 1 share users withdraw
        skip(7 hours);

        for (uint i = 0; i < 10; ++i) {               // magic value to avoid collisions
            address newUser = makeAddr(vm.toString(i + 34534534534545));
            deal(address(PIN), newUser, 1000000);
            vm.startPrank(newUser);
            pshop.claimRewards(address(fractions), 77777);
            assertEq(USDC.balanceOf(newUser), 100); // each user gets 100 wei of USDC as reward
        }

        // each user gets 100 wei of USDC
        // extrapolate that to 9999 users each with one fraction of an asset
        // I tried to do this with a loop but ran out of gas and memory, but the premise holds true
        // 9999 * 100 = 999900 total rewards distributed
        //             1009898 initial rewards provided
        //                9998 rewards stuck in contract afterwards
        // 9998 / 1009898 = ~0.0099
        // just under 1%, well in excess of the 0.01% invariant specified in the readme

        uint256 totalDistribution = 100 * 9999;
        uint256 depositedRewards = 1_009898;
        uint256 remainingRewards = depositedRewards - totalDistribution;

        // multiplying by 1e18 for the sake of precision
        uint256 percentLost = (remainingRewards * 1e18) / depositedRewards;
        assertEq(percentLost, 0.009900009703950299 ether); // 1 ether == 100% in this case
    }
```

### Mitigation

One simple way to mitigate this would be to refund any amount of USDC remains after dividing the amount of USDC by the supply of the fractional token. It would be impossible for stuck funds to occur this way, and additional rewards could always be added in the next round.